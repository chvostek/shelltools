#!/usr/bin/awk -f

BEGIN {
  r=0
  OFS=FS=","
}

function fcwarning() {
    printf "ERROR: field count does not match in %s(%d) ... YMMV.\n", FILENAME, FNR > "/dev/stderr"
}

FNR==1 {
  if (NR>1 && NF != length(h)) fcwarning()
  delete h
  for (i=1; i<=NF; i++) {
    h[i]=$i
  }
  next
}

{
  if (NF != length(h)) fcwarning()
  r++
  for (i=1; i<=NF; i++) {
    output[r,h[i]]=$i
  }
}

END {
  sep=""
  for (i=1; i<=length(h); i++) {
    printf "%s%s", sep, h[i]
    sep=OFS
  }
  printf "%s", ORS
  for (o=1; o<=r; o++) {
    sep=""
    for (i=1; i<=length(h); i++) {
      printf "%s%s", sep, output[o,h[i]]
      sep=OFS
    }
    printf "%s", ORS
  }
}

